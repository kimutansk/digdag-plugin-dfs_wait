plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id 'findbugs'
    id 'jacoco'
    id 'net.researchgate.release' version '2.6.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

group = 'com.github.kimutansk'

def digdagVersion = '0.9.21'

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

repositories {
    mavenCentral()
    jcenter()

    maven {
        url 'https://dl.bintray.com/digdag/maven'
    }
}

configurations {
    provided
}

dependencies {
    compile group: 'org.apache.hadoop', name: 'hadoop-client-runtime', version: '3.0.0'
    provided 'io.digdag:digdag-spi:' + digdagVersion
    provided 'io.digdag:digdag-standards:' + digdagVersion
    provided 'io.digdag:digdag-plugin-utils:' + digdagVersion

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

sourceSets {
    main {
        compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }
}

findbugs {
    effort = 'max'
    reportLevel = 'high'
    ignoreFailures = true
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacocoTestReport {
}

// "./gradlew check" runs jacocoTestReport
check.dependsOn('jacocoTestReport')

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

javadoc {
    options {
        locale = 'en_US'
        encoding = 'UTF-8'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}
